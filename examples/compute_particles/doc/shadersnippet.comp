/*
  This file is part of KDGpu.

  SPDX-FileCopyrightText: 2023 Klar√§lvdalens Datakonsult AB, a KDAB Group company <info@kdab.com>

  SPDX-License-Identifier: MIT

  Contact KDAB at <info@kdab.com> for commercial licensing options.
*/
#version 450

//![2]
layout (local_size_x = 256) in;
//![2]

//![1]
struct ParticleData
{
    vec4 position;
    vec4 velocity;
    vec4 color;
};

// Particles from previous frame
layout (std430, set = 0, binding = 0) coherent buffer Particles
{
    ParticleData particles[];
} data;

const float particleStep = 0.01;
const float finalCollisionFactor = 0.01;

void main(void)
{
    uint globalId = gl_GlobalInvocationID.x;

    // Retrieve current particle from previous frame
    ParticleData currentParticle = data.particles[globalId];

    // New position = old position + distance traveled over step duration
    currentParticle.position = currentParticle.position + currentParticle.velocity * particleStep;

    // Make acceleration more or less point toward the center of the scene
    vec4 acceleration =  normalize(vec4(0.0) - currentParticle.position) * finalCollisionFactor;

    // New velocity = old velocity + acceleration over step duration
    currentParticle.velocity = currentParticle.velocity + acceleration * particleStep;

    // Save updated particle
    data.particles[globalId] = currentParticle;
}
//![1]
